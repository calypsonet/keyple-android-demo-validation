plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
}

def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionBeta = 1 //Use 99 if beta is over and going to production.

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "org.eclipse.keyple.famoco.validator"
        minSdkVersion 19
        targetSdkVersion 29
        versionCode versionMajor * 1000000 + versionMinor * 10000 + versionPatch * 100 + versionBeta
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            versionNameSuffix "-DEBUG"
            applicationIdSuffix ".debug"
            resValue "string", "app_name", "Keyple Demo Validator (Debug)"
        }

        release {
            debuggable false
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue "string", "app_name", "Keyple Demo Validator"
        }
    }

    flavorDimensions "device"
    productFlavors {
        famoco {
            dimension "device"
        }
        noFamoco {
            dimension "device"
            applicationIdSuffix ".noFamoco"
            versionNameSuffix "-noFamoco"
        }
    }

    androidExtensions {
        experimental = true
    }

    //For lambda of legacy code
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        famoco.java.srcDirs += 'src/famoco/kotlin'
        noFamoco.java.srcDirs += 'src/noFamoco/kotlin'
    }

    packagingOptions {
        exclude 'META-INF/NOTICE.md'
    }

    dataBinding {
        enabled = true
    }


}

dependencies {
    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation "androidx.constraintlayout:constraintlayout:$constraintlayout_version"
    implementation "com.google.android.material:material:1.1.0-rc02"

    //Kotlin
    implementation "androidx.core:core-ktx:$core_ktx_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    //Keyple
    implementation "org.eclipse.keyple:keyple-java-core:$keyple_version"
    implementation "org.eclipse.keyple:keyple-java-calypso:$keyple_version"
    implementation "org.eclipse.keyple:keyple-android-plugin-nfc:$keyple_version"
    //implementation "org.eclipse.keyple:keyple-android-plugin-famoco-se-communication:1.0.1@aar"
    //keyple-android-plugin-famoco-se-communication
    implementation project(':keyple-android-plugin-famoco-se-communication')
    //implementation "org.eclipse.keyple:famoco-se-communication:$keyple_version"

    // Android Architecture components dependencies
    implementation "androidx.lifecycle:lifecycle-runtime:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    annotationProcessor "androidx.lifecycle:lifecycle-compiler:$lifecycle_version"

    // RxJava
    implementation "io.reactivex.rxjava2:rxjava:$rxjavaVersion"
    implementation "io.reactivex.rxjava2:rxandroid:$rxandroidVersion"

    // Dagger dependencies
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$daggerVersion"
    annotationProcessor "com.google.dagger:dagger-android-processor:$daggerVersion"
    implementation "com.google.dagger:dagger:$daggerVersion"
    implementation "com.google.dagger:dagger-android:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"
    compileOnly 'org.glassfish:javax.annotation:10.0-b28'

    // Lottie for animation
    implementation "com.airbnb.android:lottie:2.5.0"

    implementation "com.jakewharton.timber:timber:$timber_version" //Android

    testImplementation "junit:junit:$junit_version"

    testImplementation 'junit:junit:4.12'
    androidTestImplementation "androidx.test.ext:junit:$junit_androidx_ext_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_core_version"
}
